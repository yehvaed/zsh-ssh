#!/usr/bin/env zsh

# Better completion for ssh in Zsh.
# https://github.com/sunlei/zsh-ssh
# v0.0.2
# Copyright (c) 2020 Sunlei <guizaicn@gmail.com>

# # https://wiki.zshell.dev/community/zsh_plugin_standard#zero-handling
# 0="${ZERO:-${${0:#$ZSH_ARGZERO}:-${(%):-%N}}}"
# 0="${${(M)0:#/*}:-$PWD/$0}"

_ssh-host-list() {
  local ssh_config host_list
  pushd "$HOME/.ssh" >/dev/null

  ssh_config=$(command awk -f "${Plugins[ZSH_SSH_PLUGIN_DIR]}/scripts/bundle_ssh_config_files.awk" config)
  ssh_config=$(echo $ssh_config | command grep -v -E "^\s*#[^_]")

  popd >/dev/null

  host_list=$(echo $ssh_config | command awk -f "${Plugins[ZSH_SSH_PLUGIN_DIR]}/scripts/parse_ssh_config_file.awk" )

  if [ -n "$1" ]; then
    host_list=$(command grep -i "$1" <<< "$host_list")
  fi

  host_list=$(echo $host_list | command sort -u)

  echo $host_list
}


_fzf-list-generator() {
  local header host_list

  if [ -n "$1" ]; then
    host_list="$1"
  else
    host_list=$(_ssh-host-list)
  fi

  header="
Alias|->|Hostname|Desc
─────|──|────────|────
"

  host_list="${header}\n${host_list}"

  echo $host_list | command column -t -s '|'
}

_set-lbuffer() {
  local result selected_host connect_cmd is_fzf_result
  result="$1"
  is_fzf_result="$2"

  if [ "$is_fzf_result" = false ] ; then
    result=$(cut -f 1 -d "|" <<< ${result})
  fi

  selected_host=$(cut -f 1 -d " " <<< ${result})
  connect_cmd="ssh ${selected_host}"

  LBUFFER="$connect_cmd"
}

fzf-complete-ssh() {
  local tokens cmd result selected_host
  setopt localoptions noshwordsplit noksh_arrays noposixbuiltins

  tokens=(${(z)LBUFFER})
  cmd=${tokens[1]}

  if [[ "$LBUFFER" =~ "^ *ssh$" ]]; then
    zle ${fzf_ssh_default_completion:-expand-or-complete}
  elif [[ "$cmd" == "ssh" ]]; then
    result=$(_ssh-host-list ${tokens[2, -1]})

    if [ -z "$result" ]; then
      zle ${fzf_ssh_default_completion:-expand-or-complete}
      return
    fi

    if [ $(echo $result | wc -l) -eq 1 ]; then
      _set-lbuffer $result false
      zle reset-prompt
      # zle redisplay
      return
    fi

    local args=()

    if [[ "${ZSH_SSH_HOST_PREVIEW_ENABLED}" == "true" ]]; then
      args+=(
        --preview 'ssh -T -G $(cut -f 1 -d " " <<< {}) | grep -i -E "^User |^HostName |^Port |^ControlMaster |^ForwardAgent |^LocalForward |^IdentityFile |^RemoteForward |^ProxyCommand |^ProxyJump " | column -t' \
        --preview-window=right:40%
      )
    fi

    result=$(_fzf-list-generator $result | fzf \
      --height 60% \
      --ansi \
      --border \
      --cycle \
      --header-lines=2 \
      --reverse \
      --prompt='ssh > ' \
      --no-separator \
      --bind 'shift-tab:up,tab:down,bspace:backward-delete-char/eof' \
      ${args[*]}
    )

    if [ -n "$result" ]; then
      _set-lbuffer $result true
      zle accept-line
    fi

    zle reset-prompt
    # zle redisplay

  # Fall back to default completion
  else
    zle ${fzf_ssh_default_completion:-expand-or-complete}
  fi
}

fzf-complete-ssh $@